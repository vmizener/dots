###########
# Init work

# Break early on non-interactive shell
[ -z "$PS1" ] && return

# Set umask to give group write permissions
# (Default is 022)
umask 002

# Enable extended globbing
setopt EXTENDED_GLOB

# Build path
PATH=.
PATH=$PATH:${HOME}/.local/bin
PATH=$PATH:/usr/local/sbin
PATH=$PATH:/usr/local/bin
PATH=$PATH:/usr/sbin
PATH=$PATH:/usr/bin
PATH=$PATH:/sbin
PATH=$PATH:/bin

if [ ! -z $VIRTUAL_ENV ]; then
    PATH=${VIRTUAL_ENV}/bin:$PATH
fi
for d (
    /opt/X11/bin
    /Library/TeX/texbin
    ${HOME}/.pyenv/bin
) do;
    [ -d $d ] && PATH=$PATH:$d
done

export PATH


# Enable history
setopt INC_APPEND_HISTORY
export HISTSIZE=10000
export SAVEHIST=10000
export HISTFILE=${HOME}/.zsh_history

# Set default editor
export EDITOR='vim'
command -v nvim >/dev/null && export EDITOR='nvim'

# Color variables for convenience
# To print all zshrc colors:
# for i in {1..256}; do print -P "%F{$i}Color : $i"; done;
local c_blk='%F{0}'     # Soft Black
local c_red='%F{1}'     # Soft Red
local c_grn='%F{2}'     # Soft Green
local c_ylw='%F{3}'     # Soft Yellow
local c_blu='%F{4}'     # Soft Blue
local c_mag='%F{5}'     # Soft Magenta
local c_cyn='%F{6}'     # Soft Cyan
local c_wht='%F{7}'     # Soft White
local C_blk='%F{8}'     # Emph Black
local C_red='%F{9}'     # Emph Red
local C_grn='%F{10}'    # Emph Green
local C_ylw='%F{11}'    # Emph Yellow
local C_blu='%F{12}'    # Emph Blue
local C_mag='%F{13}'    # Emph Magenta
local C_cyn='%F{14}'    # Emph Cyan
local C_wht='%F{15}'    # Emph White
local c_reset='%f'      # Reset color


###########
# Startup
ZSHCONFIGFILES=(${HOME}/.config/zsh/(^submodules/)#*.zsh)  # Ignore submodule files

# Load path files
for file in ${(M)ZSHCONFIGFILES:#*/path.zsh}; do
    source ${file}
done

# Load non-path/non-completion files
for file in ${${ZSHCONFIGFILES:#*/path.zsh}:#*/completion.zsh}; do
    source ${file}
done

# Enable autocomplete
zmodload zsh/complist
bindkey -M menuselect '^[[Z' reverse-menu-complete  # Support shift-tab
autoload -U compinit && compinit

# Load completion files
for file in ${(M)ZSHCONFIGFILES:#*/completion.zsh}; do
    source ${file}
done

# Source submodules
ZSH_SUBMODS=${HOME}/.config/zsh/submodules
ZSH_PLUGINS=(
    ${ZSH_SUBMODS}/zsh-autosuggestions/zsh-autosuggestions.zsh
    ${ZSH_SUBMODS}/zsh-fzf-history-search/zsh-fzf-history-search.zsh
    # Following two plugins must be last, in this order:
    ${ZSH_SUBMODS}/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
    ${ZSH_SUBMODS}/zsh-history-substring-search/zsh-history-substring-search.zsh
)
for file in ${ZSH_PLUGINS}; do
    if [[ -e ${file} ]]; then
        source ${file}
    fi
done
# Submodule specific settings
export ZSH_FZF_HISTORY_SEARCH_FZF_ARGS='+s +m'      # Allow fuzzy searching
export ZSH_FZF_HISTORY_SEARCH_END_OF_LINE='y'       # Put cursor at end of line after select
export HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE='y'   # Unique history results only

# Bind history search keys
bindkey '^[[A' history-substring-search-up          # Up arrow
bindkey '^[[B' history-substring-search-down        # Down arrow
bindkey '^k' history-substring-search-up            # Ctrl+k
bindkey '^j' history-substring-search-down          # Ctrl+j
bindkey '^ ' autosuggest-accept                     # Ctrl+Space
bindkey -M vicmd 'k' history-substring-search-up    # k in vi-mode
bindkey -M vicmd 'j' history-substring-search-down  # j in vi-mode

# Cleanup
unset ZSHCONFIGFILES ZSH_SUBMODS ZSH_PLUGINS

# Load local init
if [[ -e ${HOME}/.localrc ]]; then
    source ${HOME}/.localrc
fi

# TODO: tab-completion not from initial character
