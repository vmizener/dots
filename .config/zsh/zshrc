###########
# Init work

# Break early on non-interactive shell
[ -z "$PS1" ] && return

# Set umask to give group write permissions
# (Default is 022)
umask 002

# Color variables for convenience
# To print all zshrc colors:
# for i in {1..256}; do print -P "%F{$i}Color : $i"; done;
local c_blk='%F{8}'     # Text Black
local c_red='%F{1}'     # Text Red
local c_grn='%F{5}'     # Text Green
local c_ylw='%F{3}'     # Text Yellow
local c_blu='%F{4}'     # Text Blue
local c_cyn='%F{6}'     # Text Cyan
local c_wht='%F{7}'     # Text White
local c_gry='%F{10}'    # Text Grey
local c_palegrey='%F{243}'
local c_reset='%f'        # Reset color

# Build path
PATH=.
PATH=$PATH:/usr/local/sbin
PATH=$PATH:/usr/local/bin
PATH=$PATH:/usr/sbin
PATH=$PATH:/usr/bin
PATH=$PATH:/sbin
PATH=$PATH:/bin

if [ ! -z $VIRTUAL_ENV ]; then
    PATH=${VIRTUAL_ENV}/bin:$PATH
fi
for d (
    /opt/X11/bin
    /Library/TeX/texbin
    ${HOME}/.pyenv/bin
) do;
    [ -d $d ] && PATH=$PATH:$d
done

export PATH


# Enable history
export HISTSIZE=10000
export SAVEHIST=10000
export HISTFILE=${HOME}/.zsh_history


###########
# Startup
ZSHCONFIGFILES=(${HOME}/.config/zsh/**/*.zsh)

# Load path files
for file in ${(M)ZSHCONFIGFILES:#*/path.zsh}; do
    source ${file}
done

# Load non-path/non-completion files
for file in ${${ZSHCONFIGFILES:#*/path.zsh}:#*/completion.zsh}; do
    source ${file}
done

# Enable autocomplete
zmodload zsh/complist
bindkey -M menuselect '^[[Z' reverse-menu-complete  # Support shift-tab
autoload -U compinit && compinit

# Load completion files
for file in ${(M)ZSHCONFIGFILES:#*/completion.zsh}; do
    source ${file}
done

unset ZSHCONFIGFILES

# Load local init
if [[ -e ${HOME}/.localrc ]]; then
    source ${HOME}/.localrc
fi

# TODO: syntax highlighting: https://github.com/zsh-users/zsh-syntax-highlighting
# TODO: tab-completion not from initial character
